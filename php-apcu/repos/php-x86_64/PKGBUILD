# $Id: PKGBUILD 281656 2016-11-20 10:00:45Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apcu
pkgver=5.1.7
pkgrel=1.1
arch=('i686' 'x86_64')
pkgdesc='A userland caching module for PHP'
url='https://pecl.php.net/package/APCu'
depends=('php')
license=('PHP')
source=("https://pecl.php.net/get/apcu-$pkgver.tgz"
        'apcu.ini')
backup=('etc/php/conf.d/apcu.ini')
md5sums=('7803b58fab6ecfe847ef5b9be6825dea'
         'a353f7efe61928aa2162720789fa171f')

build() {
	cd $srcdir/apcu-$pkgver

	phpize
	./configure --prefix=/usr
	make
}

check() {
	cd $srcdir/apcu-$pkgver

	export REPORT_EXIT_STATUS=1
	export NO_INTERACTION=1
	export SKIP_ONLINE_TESTS=1
	export SKIP_SLOW_TESTS=1
	make test
}

package() {
	cd $srcdir/apcu-$pkgver

	make INSTALL_ROOT=$pkgdir install

	install -D -m644 $srcdir/apcu.ini $pkgdir/etc/php/conf.d/apcu.ini
	install -D -m644 apc.php $pkgdir/usr/share/webapps/php-apcu/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apcu/install.txt
}
