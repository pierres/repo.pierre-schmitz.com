# $Id: PKGBUILD 251773 2015-11-21 08:10:31Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apcu
pkgver=5.1.0
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='A userland caching module for PHP'
url='http://pecl.php.net/package/APCu'
depends=('php')
license=('PHP')
source=("http://pecl.php.net/get/apcu-$pkgver.tgz")
backup=('etc/php/conf.d/apcu.ini')
md5sums=('8fc4f2a9a2447c760ee826e1786725d7')

build() {
	cd $srcdir/apcu-$pkgver
	phpize
	./configure --prefix=/usr --enable-apcu-bc
	make
}

check() {
	cd $srcdir/apcu-$pkgver
	export REPORT_EXIT_STATUS=1
	export NO_INTERACTION=1
	export SKIP_ONLINE_TESTS=1
	export SKIP_SLOW_TESTS=1
	make test
}

package() {
	cd $srcdir/apcu-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';extension=apcu.so' > apcu.ini
	install -D -m644 apcu.ini $pkgdir/etc/php/conf.d/apcu.ini
	install -D -m644 apc.php $pkgdir/usr/share/webapps/php-apcu/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apcu/install.txt
}
