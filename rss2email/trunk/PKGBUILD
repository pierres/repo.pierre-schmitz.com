# Maintainer: Chris Brannon <chris@the-brannons.com>
# Contributor: Dag Odenhall <dag.odenhall@gmail.com>
pkgname=rss2email
pkgver=2.69
pkgrel=1
pkgdesc="A tool for delivering news from RSS feeds to your email program"
arch=('any')
url="http://www.allthingsrss.com/rss2email"
license=('GPL')
depends=('python2' 'python-html2text' 'python-feedparser')
source=("http://www.allthingsrss.com/$pkgname/$pkgname-$pkgver.tar.gz"
        r2e.sh)
md5sums=('313545f4bf317195bce4b7a5559065a9'
         '75c2df5185c7bb09adfb293338cd33cc')

build() {
  /bin/true
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -D $pkgname.py "$pkgdir/usr/share/$pkgname/bin/$pkgname.py"
  install -D config.py.example "$pkgdir/usr/share/$pkgname/config.py.example"
  install -Dm644 readme.html "$pkgdir/usr/share/doc/$pkgname/readme.html"
  install -m755 -D "$srcdir/r2e.sh" "$pkgdir/usr/bin/r2e"
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

# vim:set ts=2 sw=2 et:
