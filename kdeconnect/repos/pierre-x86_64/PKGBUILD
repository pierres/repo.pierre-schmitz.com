# Maintainer: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>
# https://github.com/zizzfizzix/pkgbuilds

##############################################################
#### The section below can be adjusted to suit your needs ####
##############################################################

# What type of build do you want?
# See http://techbase.kde.org/Development/CMake/Addons_for_KDE#Buildtypes to check what is supported.
# Default is Release.

_buildtype='Release'

##############################################################

pkgname=kdeconnect
pkgver=0.4.2
pkgrel=1
pkgdesc='KDE Connect kded and kcm'
url='https://albertvaka.wordpress.com/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdelibs' 'qjson' 'qca-ossl')
makedepends=('automoc4' 'cmake')
provides=('kdeconnect')
conflicts=('kdeconnect-git')
install=kdeconnect.install
source=("http://download.kde.org/unstable/kdeconnect/${pkgver}/src/${pkgname}-kde-${pkgver}.tar.xz")
md5sums=('96320feda9805296c72e1e10578b903e')

if [[ ! ${_buildtype} == 'Release' ]] && [[ ! ${_buildtype} == 'release' ]]; then
  options=(debug)
fi

prepare() {
  if [[ -e ${pkgname}-kde-${pkgver}-build ]]; then rm -rf ${pkgname}-kde-${pkgver}-build; fi
  mkdir ${pkgname}-kde-${pkgver}-build
}

build() {
  cd ${pkgname}-kde-${pkgver}-build

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE4_BUILD_TESTS=OFF \
        -DCMAKE_BUILD_TYPE=${_buildtype} \
        ../${pkgname}-kde-${pkgver}
  make
}

package() {
  cd ${pkgname}-kde-${pkgver}-build
  make DESTDIR=${pkgdir} install
}
