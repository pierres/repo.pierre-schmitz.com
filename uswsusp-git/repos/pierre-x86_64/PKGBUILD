# Maintainer: cantabile <cantabile dot desu at gmail dot com>

_appname=uswsusp
_suffix=-git
pkgname=${_appname}${_suffix}
pkgver=20101217
pkgrel=1
pkgdesc="Userspace software suspend - latest code from git."
arch=('i686' 'x86_64')
url='http://suspend.sourceforge.net/'
license=('GPL')
depends=('libgcrypt' 'libx86' 'lzo2')
makedepends=('git' 'libgcrypt')
provides=("$_appname")
conflicts=("$_appname")
backup=('etc/suspend.conf')
install=${_appname}.install
source=('uresume-hook'
        'uresume-install')

_gitroot="git://git.kernel.org/pub/scm/linux/kernel/git/rafael/suspend-utils.git"
_gitname="$_appname"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [ -d $_gitname ] ; then
    cd $_gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $_gitroot $_gitname
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #

  ./autogen.sh
  ./configure \
     --prefix=/usr \
     --enable-compress \
     --enable-encrypt \
     --enable-threads \
     --disable-resume-static \
     --disable-static \
     --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install

  install -D -m644 "$srcdir/uresume-hook" "$pkgdir/lib/initcpio/hooks/uresume"
  install -D -m644 "$srcdir/uresume-install" "$pkgdir/lib/initcpio/install/uresume"
} 

sha256sums=('b7f81d576775135f51d0c85afdc0bc475a219858c6dfdbb269551a3a064284d4'
            '909794145fe4268235095a72461bc350d946da2518ca7432d41407be10d9e391')
