# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-unit
pkgver=3.5.14
pkgrel=1
arch=('any')
pkgdesc='Regression testing framework for unit tests.'
url='http://www.phpunit.de/'
license='BSD'
source=("http://pear.phpunit.de/get/PHPUnit-${pkgver}.tgz")
depends=('php')
md5sums=('0c0e6b7ecbdf85a01867e89e07e2c143')

build() {
	cd ${srcdir}/PHPUnit-${pkgver}

	sed -e 's#@php_bin@#/usr/bin/php#g' -i phpunit.php
	sed -e 's#@php_bin@#/usr/bin/php#g' -i PHPUnit/Util/Fileloader.php
	sed -e 's#public static $phpBinary = NULL;#public static $phpBinary = "/usr/bin/php";#g' \
		-i PHPUnit/Util/Fileloader.php
}

package() {
	cd ${srcdir}/PHPUnit-${pkgver}
	install -D -m755 phpunit.php ${pkgdir}/usr/share/pear/PHPUnit/phpunit.php
	cp -dR PHPUnit ${pkgdir}/usr/share/pear/
	install -d -m755 ${pkgdir}/usr/bin/
	ln -sf /usr/share/pear/PHPUnit/phpunit.php ${pkgdir}/usr/bin/phpunit
}
