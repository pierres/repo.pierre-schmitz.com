# $Id: PKGBUILD 224226 2014-10-12 06:49:11Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-ast
pkgver=0.1.1
pkgrel=3
arch=('i686' 'x86_64')
pkgdesc='Extension exposing PHP 7 abstract syntax tree'
url='https://github.com/nikic/php-ast'
depends=('php')
makedepends=('git')
license=('PHP')
source=("${pkgname}::git+https://github.com/nikic/php-ast.git#tag=v${pkgver}")
backup=('etc/php/conf.d/ast.ini')
md5sums=('SKIP')

build() {
	cd ${srcdir}/${pkgname}

	phpize
	./configure --prefix=/usr
	make
}

check() {
	cd ${srcdir}/${pkgname}

	export REPORT_EXIT_STATUS=1
	export NO_INTERACTION=1
	export SKIP_ONLINE_TESTS=1
	export SKIP_SLOW_TESTS=1
	make test
}

package() {
	cd ${srcdir}/${pkgname}

	make INSTALL_ROOT=$pkgdir install
	echo ';extension=ast.so' > ast.ini
	install -D -m644 ast.ini $pkgdir/etc/php/conf.d/ast.ini
}
