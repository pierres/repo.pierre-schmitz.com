# $Id: PKGBUILD 156068 2012-04-12 18:41:18Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apc
pkgver=3.1.10
pkgrel=1.1
arch=('i686' 'x86_64')
pkgdesc='A free, open, and robust framework for caching and optimizing PHP intermediate code'
url='http://pecl.php.net/package/APC'
depends=('php')
license=('PHP')
source=("http://pecl.php.net/get/APC-${pkgver}.tgz")
backup=('etc/php/conf.d/apc.ini')
md5sums=('f4a6b91903d6ba9dce89fc87bb6f26c9')

build() {
	cd $srcdir/APC-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

# check() {
# 	cd $srcdir/APC-$pkgver
# 	make test
# }

package() {
	cd $srcdir/APC-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';extension=apc.so' > apc.ini
	install -D -m644 apc.ini $pkgdir/etc/php/conf.d/apc.ini
	install -D -m644 apc.php $pkgdir/usr/share/php-apc/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apc/install.txt
}
