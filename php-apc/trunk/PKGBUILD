# $Id: PKGBUILD 135862 2011-08-19 20:05:54Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apc
pkgver=3.1.9
pkgrel=5
arch=('i686' 'x86_64')
pkgdesc='A free, open, and robust framework for caching and optimizing PHP intermediate code'
url='http://pecl.php.net/package/APC'
depends=('php')
license=('PHP')
source=("http://pecl.php.net/get/APC-${pkgver}.tgz"
        'svn-323991.patch')
backup=('etc/php/conf.d/apc.ini')
md5sums=('a2cf7fbf6f3a87f190d897a53260ddaa'
         'a2aa0a0d1a2764b06d218f4f56b7d9df')

build() {
	cd $srcdir/APC-$pkgver
	patch -p0 -i ${srcdir}/svn-323991.patch
	phpize
	./configure --prefix=/usr
	make
}

package() {
	cd $srcdir/APC-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';extension=apc.so' > apc.ini
	install -D -m644 apc.ini $pkgdir/etc/php/conf.d/apc.ini
	install -D -m644 apc.php $pkgdir/usr/share/php-apc/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apc/install.txt
}
