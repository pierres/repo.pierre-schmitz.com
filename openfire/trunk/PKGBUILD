# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=openfire
pkgver=3.6.4
pkgrel=4
pkgdesc="A high performance XMPP (Jabber) server"
arch=('any')
url='http://www.igniterealtime.org/projects/openfire/'
license=('GPL')
depends=('openjdk6' 'bash')
makedepends=('apache-ant')
install='openfire.install'
# Todo: logrotate
backup=('opt/openfire/conf/openfire.xml' 'etc/conf.d/openfire' 'opt/openfire/resources/security/keystore')
source=("http://www.igniterealtime.org/downloadServlet?filename=openfire/openfire_src_${pkgver//./_}.tar.gz"
        'openfire' 'openfire.conf' 'ant.patch')
md5sums=('0b5417368355045afbbfac4155efd988'
         '28d0f56a2230c79b29f1d8ec1c31df67'
         '70727798fbcc3e23c10af81c73287791'
         '172094d531eba3459d079e6dac069cba')

build() {
	cd ${srcdir}/openfire_src/build
	patch -p0 -i ${srcdir}/ant.patch
	rm -rf ../src/resources/nativeAuth/
	ant openfire
# 	ant plugins
}

package() {
	install -d -m755 ${pkgdir}/opt/openfire
	install -d -m750 ${pkgdir}/var/log/openfire
	install -D -m644 $srcdir/openfire.conf ${pkgdir}/etc/conf.d/openfire
	install -D -m774 $srcdir/openfire ${pkgdir}/etc/rc.d/openfire
	cd ${srcdir}/openfire_src/target/openfire
	rm -rf bin logs
	mv * ${pkgdir}/opt/openfire/
}
