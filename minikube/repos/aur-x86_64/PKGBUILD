# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=minikube
pkgver=1.12.1
pkgrel=1.1
pkgdesc="A tool that makes it easy to run Kubernetes locally"
url="https://github.com/kubernetes/minikube"
license=('Apache')
arch=('x86_64')
optdepends=(
  'kubectl: to manage the cluster'
  'virtualbox: to use --vm-driver=virtualbox'
)
makedepends=('git' 'go' 'go-bindata')
depends=('glibc')
source=("minikube::git+https://github.com/kubernetes/minikube.git#tag=v${pkgver}"
        "minikube-docker-mount-${pkgver}-${pkgrel}.patch::https://patch-diff.githubusercontent.com/raw/kubernetes/minikube/pull/8136.patch")
md5sums=('SKIP'
         '4ddc489965df6769b43339a8acf9f8de')

prepare() {
  rm -rf "$srcdir/src"
  mkdir -p "$srcdir/src/k8s.io"
  mv -T "$srcdir/minikube" "$srcdir/src/k8s.io/minikube"

  cd $srcdir/src/k8s.io/minikube
  patch -p1 -i $srcdir/minikube-docker-mount-${pkgver}-${pkgrel}.patch
}

build() {
  export GOPATH="$srcdir"
  cd "$GOPATH/src/k8s.io/minikube"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  install -Dm755 "$srcdir/src/k8s.io/minikube/out/minikube" -t "$pkgdir/usr/bin"

  "$pkgdir/usr/bin/minikube" completion bash | install -Dm644 /dev/stdin "$pkgdir/usr/share/bash-completion/completions/minikube"
  "$pkgdir/usr/bin/minikube" completion zsh | install -Dm644 /dev/stdin "$pkgdir/usr/share/zsh/site-functions/_minikube"
}
