# $Id: PKGBUILD 146960 2012-01-19 17:53:37Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php53-suhosin
pkgver=0.9.33
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='An advanced protection system for PHP installations'
url='http://www.hardened-php.net/suhosin/'
license='PHP'
source=("http://download.suhosin.org/suhosin-${pkgver}.tgz"
        "http://download.suhosin.org/suhosin-${pkgver}.tgz.sig")
depends=('php53')
conflicts=('php-suhosin-extension' 'php-suhosin')
provides=("php-suhosin=${pkgver}")
replaces=('php-suhosin-extension')
backup=('etc/php/conf.d/suhosin.ini')
md5sums=('0ce498a02a8281e4274ea8e390c2b487'
         '6e9536cc71342bab0efb1e9a84f688a6')

build() {
	cd ${srcdir}/suhosin-${pkgver}
	phpize
	./configure --prefix=/usr --enable-suhosin
	make
}

package() {
	cd ${srcdir}/suhosin-${pkgver}
	make INSTALL_ROOT=${pkgdir} install
	# disable by default
	sed -i 's|extension = suhosin.so|;extension=suhosin.so|g' suhosin.ini
	install -D -m644 suhosin.ini ${pkgdir}/etc/php/conf.d/suhosin.ini
}
