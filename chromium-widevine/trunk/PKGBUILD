# Maintainer: Doug Newgard <scimmia at archlinux dot info>

pkgname=chromium-widevine
pkgdesc='A browser plugin designed for the viewing of premium video content'
_chromever=$(curl -s https://omahaproxy.appspot.com/all | awk -F , '/linux,stable/ {print $3}')
pkgver=1.4.7.796
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.google.com/chrome'
license=('custom:chrome')
options=('!strip')
source=('https://www.google.com/chrome/intl/en/eula_text.html')
source_i686=("google-chrome-stable_${_chromever}_i386.deb::https://dl.google.com/linux/direct/google-chrome-stable_current_i386.deb")
source_x86_64=("google-chrome-stable_${_chromever}_amd64.deb::https://dl.google.com/linux/direct/google-chrome-stable_current_amd64.deb")
sha256sums=('c714679be9b1de35475bfcd58b4d2fa5baf87c00eb049185ec062de276203d13')
sha256sums_i686=('c7fbd6f7ca71183ff998f3d83a00f7d79f51cf92eaf3c42d6265a0b5f11f7c0d')
sha256sums_x86_64=('4d0612b5a1a0f6588dc6af104dacba729d970f2c8c5332071241c104208a5cad')

pkgver() {
  bsdtar -xf data.tar.xz opt/google/chrome/{chrome,libwidevinecdm{,adapter}.so}
  printf "$(strings opt/google/chrome/chrome | grep -C 1 " (version:" | tail -1)"
}

package() {
  install -d "$pkgdir/usr/lib/chromium/"
  install -m644 opt/google/chrome/*.so "$pkgdir/usr/lib/chromium/"
  install -Dm644 eula_text.html "$pkgdir/usr/share/licenses/$pkgname/eula_text.html"
}
