# Maintainer: Jamie Nguyen <jamie AT tomoyolinux.co.uk>

pkgname=webalizer
pkgver=2.23
_pkgver=2.23-03
pkgrel=2
pkgdesc="A web server log file analysis program producing HTML reports"
arch=('i686' 'x86_64')
url="http://www.webalizer.com/"
license=('GPL')
depends=('db' 'gd' 'geoip')
install=webalizer.install
# webalizer-geodb-latest.tgz = webalizer-geodb-20110201.tgz
source=("ftp://ftp.mrunix.net/pub/webalizer/webalizer-${_pkgver}-src.tgz"
        "ftp://ftp.mrunix.net/pub/webalizer/webalizer-geodb-latest.tgz"
        "ftp://ftp.mrunix.net/pub/webalizer/webalizer-flags.tgz")
        
build() {
  cd "${srcdir}/webalizer-${_pkgver}"

  ./configure \
	  --prefix=/usr \
	  --sysconfdir=/etc \
	  --datarootdir=/usr/share \
	  --enable-bz2 \
	  --enable-geoip \
	  || return 1

  make || return 1
}

package() {
  cd "${srcdir}/webalizer-${_pkgver}"

  # install webalizer
  make DESTDIR="${pkgdir}" install || return 1

  # install geolocation database (GeoDB)
  install -m755 -d "${pkgdir}/usr/share/GeoDB"
  install -m644 "${srcdir}/GeoDB.dat" "${pkgdir}/usr/share/GeoDB/GeoDB.dat"

  # install flags
  cd "${srcdir}/flags"
  install -m755 -d "${pkgdir}/usr/share/webalizer/flags"
  for i in *.png; do
	  install -m644 ${i} "${pkgdir}/usr/share/webalizer/flags/${i}"
  done
}

sha256sums=('91aa9c4ca817afbe535219cbb107a349eb5d1596ed763614fb01be7f9d655286'
            'cef1202ff1a9a26adb4f82e358cc58b4b560f04c0703f7617022eece603abeea'
            'df9961fec7da2c16ed3faefb763f912c2db5ddce6a24e4f46647d7e64b8ab29a')
