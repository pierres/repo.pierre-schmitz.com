# Maintainer: Byeonghoon Yoo <bh322yoo gmail com>
# Contributor: Byeonghoon Yoo <bh322yoo gmail com>

_pkgname=openvpn3-linux
pkgname=openvpn3
pkgver=21.0.287.g8a13d1b
pkgrel=1
pkgdesc='OpenVPN 3 Linux client'
arch=('x86_64' 'aarch64')
url="https://github.com/OpenVPN/$_pkgname"
license=('AGPL3')
depends=(
  'gdbuspp' 'jsoncpp>=0.10.5' 'libcap-ng>=0.7.5' 'lz4>=1.8.4' 'util-linux-libs>=2.23.2' 'protobuf>=2.4.0'
  'python-dbus' 'python-gobject' 'python-systemd' 'tinyxml2>=2.1.0' 'libnl>=3.2.29'
)
optdepends=(
  'openssl: OpenSSL version' 'mbedtls: mbed TLS version' 'polkit>=0.112: for systemd-resolved integration'
  'repkg: Automatically rebuild the package on dependency updates'
)
makedepends=(
  'autoconf-archive' 'git' 'python-docutils' 'python-jinja' 'bash' 'meson' 'pcre2' 'xz' 'libffi' 'zlib'
)
source=(
  "git+https://codeberg.org/OpenVPN/openvpn3-linux.git"
  'openvpn3.rule'
  'openvpn3.install'
  'sysusers-openvpn3.conf'
)
sha256sums=('SKIP'
            'ec0b8e28ae77b4b074d3eb8a084626e6dcfc587a07bef5d53fe1c6e160c0fc01'
            'a5879d9e637658f5127e849d17f4050bae1f82fa9b25f05bbbbb531867120e8b'
            '045e914bb6fff5a082314dfc805bb511c9a80170619fa1e94a07825fa977c90a')

pkgver() {
  cd "$_pkgname"
  git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/0.\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"
  git submodule init
  git submodule update

  meson subprojects download
}

build() {
  cd "$srcdir"
  # see https://github.com/OpenVPN/openvpn3-linux/issues/171#issuecomment-1858487477
  arch-meson $_pkgname build -Ddco=disabled -Dselinux_policy=disabled
  meson compile -C build
}

package() {
  cd "$srcdir"
  meson install -C build --destdir "$pkgdir"

  install -Dm644 "$srcdir/$_pkgname/src/shell/bash-completion/openvpn3" "$pkgdir/usr/share/bash-completion/completions/openvpn3"
  install -Dm644 "$srcdir/${pkgname}.rule" "$pkgdir/etc/repkg/rules/system/${pkgname}.rule"
  install -Dm644 "$srcdir/sysusers-$pkgname.conf"  "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}
