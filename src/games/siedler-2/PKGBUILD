# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=siedler-2
pkgver=1.51
pkgrel=5
pkgdesc="Ein Aufbaustrategiespiel von Blue Byte."
arch=('any')
url="https://www.gog.com/de/game/the_settlers_2_gold_edition"
license=('custom')
depends=('dosbox-staging' 'bash' 'soundfont-fluid')
makedepends=('innoextract' 'imagemagick')
options=('!strip' '!debug')
source=("file://./setup_the_settlers_2_gold_1.5.1_(german)_(30319).exe"
        "${pkgname}.sh" "${pkgname}.desktop" "dosbox.conf" "dosbox-s2.conf"
        "${pkgname}-editor.sh" "${pkgname}-editor.desktop" "dosbox-s2edit.conf")
sha256sums=('313fe3ab08e20f4ad4067967dbb9fa807053abb74389e9e211b627c9987faff6'
            '2992af0a7ac1e5f3ff0616c64725deed13982a84a160996f24eed144b3430874'
            'bf227916810b7ae4d32d81e8abc16569fd0d2f964c4c115e364087d384d6a770'
            'f866330e64dbe8630e5d2e5b8a3923c1627bed67d82eb48ed6e1f160cb9e8e09'
            '19cd9e6ac51463f6aaea2d785510a27f7a2770d3ad97439eced7b2436ccc9da5'
            '7dceb8107bb0c915220b6a283a8f1fc2639dfa1fc2a39efbbef1e91d02ccce69'
            '1cae5d96cf6cd02baa990193beb02c1a90f222dfe90e02adae3b8eced254b368'
            '9bb7000a713c29919a4f9c1ec551a7bd94de8f42d1a6ad23ae6bcfc47dfa9442')

package() {
    mkdir -p "$pkgdir/usr/share/${pkgname}"

    pushd "$pkgdir/usr/share/${pkgname}"
    innoextract --extract --exclude-temp "$srcdir/setup_the_settlers_2_gold_1.5.1_(german)_(30319).exe"
    mv __support/save/SAVE .
    magick app/goggame-*.ico -compress none "${pkgname}.ico"
    rm -rf __redist __support app commonappdata DOSBOX goggame-*
    mkdir WORLDS
    popd

    install -D --mode 644 "$srcdir/dosbox.conf" "${pkgdir}/usr/share/${pkgname}/dosbox.conf"

    install -D --mode 755 "$srcdir/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
    install -D --mode 644 "$srcdir/dosbox-s2.conf" "${pkgdir}/usr/share/${pkgname}/dosbox-s2.conf"
    install -D --mode 644 "$srcdir/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"

    install -D --mode 755 "$srcdir/${pkgname}-editor.sh" "${pkgdir}/usr/bin/${pkgname}-editor"
    install -D --mode 644 "$srcdir/dosbox-s2edit.conf" "${pkgdir}/usr/share/${pkgname}/dosbox-s2edit.conf"
    install -D --mode 644 "$srcdir/${pkgname}-editor.desktop" "${pkgdir}/usr/share/applications/${pkgname}-editor.desktop"
}
