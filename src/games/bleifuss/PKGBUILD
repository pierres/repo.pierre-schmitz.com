# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=bleifuss
pkgver=1.0.0
pkgrel=6
pkgdesc="Ein Rennspiel, das von Milestone 1995 entwickelt wurde"
arch=('any')
url="https://de.wikipedia.org/wiki/Bleifuss"
license=('custom')
depends=('dosbox-staging' 'bash')
makedepends=('xorg-server-xvfb' 'util-linux')
options=('!strip' '!debug')
source=("file://./${pkgname}".{cue,bin}
        "https://archive.org/download/strings-v2.5/STRING25.ZIP"
        dosbox{,-install,-start,-setup,-network-server,-network-client}.conf
        "${pkgname}.sh" "${pkgname}-setup.sh" "${pkgname}-network-server.sh" "${pkgname}-network-client.sh"
        "${pkgname}.desktop")
sha256sums=('d3a0287d3a4e5b943007d1105f93a2fa0d46fa5a8bb3c71769e2e4249a54998a'
            'ab8d2407471bf2840e01872330bd3a78797b6fb5394c0a17d3a049d1d3c8a393'
            '7fe87d3e38ab7ffb3528485c93aaece46d10a7536d72917b8d2807cffb44474b'
            'ad0df1568ab0d04be5be0c18c879cac14e956712558c2a06ee2df193193b773a'
            'd5fe68f5f612a7ff3c764d07b62cc39d510b5e170af9c6bed4e31ef448b32086'
            'b7d3bb1a1a4ab120999cb23ae48e0dd3e62fe4fe05eec222225328ce9e7653d0'
            '187f0e266a3ee2a8624bca2fe7634305a3eb819b8e82a61d189d823c0153113d'
            'fe308e744f05a252dec433dae8ebd5f6b53adc11616590f4a01ac3766e5fafc9'
            '572570e8dc9f3e5431cded3f79c1419ecc833e584666330b7408cbde13a93cba'
            'ff1cb31b8b69d35354a7aad651f7050f215a35b0a7d52ad871171812400cbc11'
            'd54b07fe9d81149bda9baeb8250b8fffa386894aee4e205d53f6883e71fd3167'
            'cde1bab512524315107cead43cb3584574f73aab4c47bf7c8a897076796d429d'
            '64715130ad252ac5c5537855a5b264aa47312c6b3e265a13b42571446df6a854'
            '44a926f8c26fcbad099682533771c01de1b046548c64aed2a3adbf1a169a04fd')

package() {
    mkdir -p "$pkgdir/usr/share/${pkgname}"

    cp "$srcdir/${pkgname}".{cue,bin} "$pkgdir/usr/share/${pkgname}"
    cp "$srcdir"/dosbox*.conf "$pkgdir/usr/share/${pkgname}"

    pushd "$pkgdir/usr/share/${pkgname}"
        Xvfb :12356 &
        xpid=$!
        export DISPLAY=:12356

        dosbox --noprimaryconf --conf dosbox-install.conf --set output=texture --set texture_renderer=software

        kill $xpid
        cp "$srcdir/STRINGS.COM" .
    popd

    chown -R root:root "$pkgdir/usr/share/${pkgname}"
    find "$pkgdir/usr/share/${pkgname}" -type f -exec chmod 0644 {} \;
    find "$pkgdir/usr/share/${pkgname}" -type d -exec chmod 0755 {} \;

    rm -f "$pkgdir/usr/share/${pkgname}"/dosbox-install.conf

    install -D --mode 755 "$srcdir/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
    install -D --mode 755 "$srcdir/${pkgname}-setup.sh" "${pkgdir}/usr/bin/${pkgname}-setup"
    install -D --mode 755 "$srcdir/${pkgname}-network-server.sh" "${pkgdir}/usr/bin/${pkgname}-network-server"
    install -D --mode 755 "$srcdir/${pkgname}-network-client.sh" "${pkgdir}/usr/bin/${pkgname}-network-client"

    install -D --mode 644 "$srcdir/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
