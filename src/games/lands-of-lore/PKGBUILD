# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=lands-of-lore
pkgver=1.0.0
pkgrel=3
pkgdesc="Lands of Lore: The Throne of Chaos"
arch=('any')
url="https://en.wikipedia.org/wiki/Lands_of_Lore:_The_Throne_of_Chaos"
license=('custom')
depends=('scummvm' 'soundfont-fluid')
options=('!strip' '!debug')
source=("file://./${pkgname}.iso"
        "${pkgname}.desktop")
sha256sums=('9d660783c84bbd67a6c9de207a3a8ca139b1366093af1f158718ca5cfafd8a32'
            '1a32a5ff111e1595b315485af41f0850f45857d84aada5df85748c367c01a825')

prepare() {
    cd "$srcdir"
    bsdtar xf "$pkgname.iso"
}

package() {
    mkdir -p "$pkgdir/usr/share/${pkgname}"

    cp -a "$srcdir/DATA/"{FILEDATA.FDT,*.PAK,*.TLK,ENG,FRE,GER,HARDRIVE/LANDS.ICO} "$pkgdir/usr/share/${pkgname}"

    chown -R root:root "$pkgdir/usr/share/${pkgname}"
    find "$pkgdir/usr/share/${pkgname}" -type f -exec chmod 0644 {} \;
    find "$pkgdir/usr/share/${pkgname}" -type d -exec chmod 0755 {} \;

    install -Dm644 "$srcdir/${pkgname}.desktop" "$pkgdir/usr/share/applications/${pkgname}.desktop"
}
