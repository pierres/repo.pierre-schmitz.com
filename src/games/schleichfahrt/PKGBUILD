# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=schleichfahrt
pkgver=1.120
pkgrel=2
pkgdesc="Ein spannendes Spiel, in dem ein futuristisches U-Boot in einer post-apokalyptischen Unterwasserwelt gegen feindliche Kräfte kämpft."
arch=('any')
url="https://web.archive.org/web/20040808052843/http://www.bluebyte.net/ger/products/schleichfahrt/index.htm"
license=('custom')
depends=('dosbox-staging' 'bash')
makedepends=('innoextract')
options=('!strip' '!debug')
source=("file://./setup_archimedean_dynasty_german_2.0.0.4.exe"
        "https://archive.org/download/FTP.BLUEBYTE.COM.2004.03/FTP.BLUEBYTE.COM.2004.03.zip"
        "${pkgname}.sh" "${pkgname}.desktop" "dosbox.conf")
sha256sums=('c59efdeeca79bef8e1e5ac9e3207acd2f5b61ecb8eccc20321fa53962b392233'
            '1aec5103e00dbdfa7858e6d0f624a469f0b6a8cd5110b5d8dd33da6f2fd2872d'
            '1c516ae7929189b52803d6abe3cf718743fbc219918065a788179df10dfacba8'
            '4b22bbed7f1b45139bc88fa1679101a7ec96ce8498a195d8abc64160bc182bc5'
            '118ec221a8177df329e0c69de51caf77e30e7731deb10bb497ac0dcb40fbda08')

package() {
    mkdir -p "$pkgdir/usr/share/${pkgname}"

    pushd "$pkgdir/usr/share/${pkgname}"
    innoextract --extract --exclude-temp "$srcdir/setup_archimedean_dynasty_german_2.0.0.4.exe"
    mv app/* .
    mv goggame-*.ico "${pkgname}.ico"
    rm -rf app __support commonappdata DOSBOX goggame* *.dll *.conf webcache.zip VRUSH
    bsdtar xf "$srcdir/updates/ger/schleichfahrt/sf_3dfx.exe" GLIDE2X.OVL
    popd

    install -D --mode 755 "$srcdir/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
    install -D --mode 644 "$srcdir/dosbox.conf" "${pkgdir}/usr/share/${pkgname}/dosbox.conf"
    install -D --mode 644 "$srcdir/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
