# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=bleifuss-fun
pkgver=6.56
pkgrel=5
pkgdesc="Ein Rennspiel, das von Milestone 1998 entwickelt wurde"
arch=('any')
url="https://de.wikipedia.org/wiki/Bleifuss_fun"
license=('custom')
depends=('dosbox-staging' 'bash')
makedepends=('xorg-server-xvfb' 'util-linux' 'icoutils')
options=('!strip' '!debug')
source=("file://./${pkgname}".{cue,bin}
        "file://./blei_win.zip"
        "file://./ble3dfx3.zip"
        "file://./ble3dfx2.zip"
        dosbox{,-install,-start}.conf
        "${pkgname}.sh"
        "${pkgname}.desktop")
sha256sums=('0c53776a94c6dc902f36723d05d2ba7b6125a4f3553f4cd85ed1cf1e7fec51ba'
            '8f249e1951e6f424a2543955ec5f7f2a0457823f752b89c8f1ad3929ec952360'
            'e98f2072b9d2af85c3db51e8f0dec162079fb059da9faf76630b948a8a0d2248'
            '035d4ba99c1a4480c5f7a427fe46068d324a71d8d6fcbf62056d91ca3a09f605'
            'a81a31f5c835b3051d94df088179a5e4b1bf48e99ef65df227d71ff91438cd4d'
            'd3cb9653fec452ecfb926872fdd9f937b9f13e097a98aa3b0c3cd4e29609a41d'
            '78ab477de9f497ae0c6b8f536327c71e3ab8852a6ebbbf7fce0b8811b43401fb'
            'f42bcca05b0233d03b5e0e4f2ba8940b7f1f8eed02b0be1c7ecc3e1082161eaa'
            '70214e4f2a4fff46fcfd0ab9dbe5b086107571857b4109bc9f96bee0e6d4b2f1'
            'cd54967df69b7a5bfe26bdd794881fee8e83235993725f95c3a511be931b35b7')

package() {
    mkdir -p "$pkgdir/usr/share/${pkgname}"

    cp "$srcdir/${pkgname}".{cue,bin} "$pkgdir/usr/share/${pkgname}"
    cp "$srcdir"/dosbox*.conf "$pkgdir/usr/share/${pkgname}"

    pushd "$pkgdir/usr/share/${pkgname}"
        Xvfb :12356 &
        xpid=$!
        export DISPLAY=:12356
        export SDL_VIDEODRIVER=x11

        dosbox --noprimaryconf --conf dosbox-install.conf --set output=texture --set texture_renderer=software

        bsdtar xf "$srcdir/blei_win.zip"
        bsdtar xf "$srcdir/ble3dfx3.zip"
        bsdtar xf "$srcdir/ble3dfx2.zip"

        wrestool -x --output=. -t14 ./Blei_win.exe
        mv Blei_win.exe_*.ico "${pkgname}.ico"

        kill $xpid
    popd

    chown -R root:root "$pkgdir/usr/share/${pkgname}"
    find "$pkgdir/usr/share/${pkgname}" -type f -exec chmod 0644 {} \;
    find "$pkgdir/usr/share/${pkgname}" -type d -exec chmod 0755 {} \;

    rm -f "$pkgdir/usr/share/${pkgname}"/dosbox-install.conf

    install -D --mode 755 "$srcdir/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

    install -D --mode 644 "$srcdir/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
