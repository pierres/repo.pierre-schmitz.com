# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=bleifuss-fun
pkgver=6.56
pkgrel=2
pkgdesc="Ein Rennspiel, das von Milestone 1998 entwickelt wurde"
arch=('any')
url="https://de.wikipedia.org/wiki/Bleifuss_fun"
license=('custom')
depends=('dosbox-staging' 'bash')
makedepends=('xorg-server-xvfb' 'util-linux' 'unzip')
options=('!strip' '!debug')
source=("file://./${pkgname}".{cue,bin}
        "file://./blei_win.zip"
        "file://./ble3dfx3.zip"
        "file://./ble3dfx2.zip"
        dosbox{,-install,-start}.conf
        "${pkgname}.sh"
        "${pkgname}.desktop")
sha256sums=('0c53776a94c6dc902f36723d05d2ba7b6125a4f3553f4cd85ed1cf1e7fec51ba'
            '8f249e1951e6f424a2543955ec5f7f2a0457823f752b89c8f1ad3929ec952360'
            'e98f2072b9d2af85c3db51e8f0dec162079fb059da9faf76630b948a8a0d2248'
            '035d4ba99c1a4480c5f7a427fe46068d324a71d8d6fcbf62056d91ca3a09f605'
            'a81a31f5c835b3051d94df088179a5e4b1bf48e99ef65df227d71ff91438cd4d'
            'c2055896fd42e508742b396d48f113a5f6e4f33a92675960fab2d95b7864c755'
            '78ab477de9f497ae0c6b8f536327c71e3ab8852a6ebbbf7fce0b8811b43401fb'
            'f42bcca05b0233d03b5e0e4f2ba8940b7f1f8eed02b0be1c7ecc3e1082161eaa'
            '70214e4f2a4fff46fcfd0ab9dbe5b086107571857b4109bc9f96bee0e6d4b2f1'
            '92efb315c985462586fcc3e89425fac91ddb77b9ecc7b72e1dac8f9e23fabd7f')

package() {
    mkdir -p "$pkgdir/usr/share/${pkgname}"

    cp "$srcdir/${pkgname}".{cue,bin} "$pkgdir/usr/share/${pkgname}"
    cp "$srcdir"/dosbox*.conf "$pkgdir/usr/share/${pkgname}"

    pushd "$pkgdir/usr/share/${pkgname}"
        Xvfb :12356 &
        xpid=$!
        export DISPLAY=:12356
        export SDL_VIDEODRIVER=x11

        dosbox --noprimaryconf --conf dosbox-install.conf --set output=texture --set texture_renderer=software

        bsdtar xf "$srcdir/blei_win.zip"
        bsdtar xf "$srcdir/ble3dfx3.zip"
        bsdtar xf "$srcdir/ble3dfx2.zip"

        kill $xpid
    popd

    chown -R root:root "$pkgdir/usr/share/${pkgname}"

    rm -f "$pkgdir/usr/share/${pkgname}"/dosbox-install.conf

    install -D --mode 755 "$srcdir/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

    install -D --mode 644 "$srcdir/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
