# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=bleifuss-rally
pkgver=1.0.0
pkgrel=3
pkgdesc="Ein Rennspiel, das von Milestone 1997 entwickelt wurde"
arch=('any')
url="https://de.wikipedia.org/wiki/Bleifuss_Rally"
license=('custom')
depends=('dosbox-staging' 'bash')
makedepends=('xorg-server-xvfb' 'util-linux' 'unzip')
options=('!strip' '!debug')
source=("file://./${pkgname}".{cue,bin}
        "file://./br3dfx.zip"
        dosbox{,-install,-start,-setup,-network-server,-network-client}.conf
        "${pkgname}.sh" "${pkgname}-setup.sh" "${pkgname}-network-server.sh" "${pkgname}-network-client.sh"
        "${pkgname}.desktop")
sha256sums=('1394d63f3a1c934cdb14be1d141b4f9163bc6cc955ae336730c39d3a5103487f'
            '2a7db1d2216d9d6f9d39b5f91422a9e9cee0284445d85111b63242ddafa966c4'
            '1bcd5b28cb37b9363099b78efd9463b08f81396bcae5bde2310e5cfb2b7c402c'
            'c2055896fd42e508742b396d48f113a5f6e4f33a92675960fab2d95b7864c755'
            'd2cd3ca693a596ff88567a6d9e280bf32212ead3f1dd43ae80ff0a9807b06de2'
            'd8db2668a6cc372194893ae0e6f01988a85f54f38d58935fbfca1678d986fbc3'
            'c403288e9811e8f4bdca03210149487a9b79eb331be9590e44d39c35b0201884'
            '881d8464f5d3529f0ce15303110ffa933435a830c50ab11ed5e25c4b808ee39c'
            '017aae89177069fcbdb3c0011260a9457f2df4da4306361847007b7c77b9fcef'
            '98a8524d26679fb03031b4864235a3b4611eb2f6ec5e5afd40ebb744e086231b'
            '5e8bee7778c247d7ce88d591d276033a0381d8a88dc1023f2dacab4410288519'
            '0beabb91f0691791a871dce501ea1853316ea9b1350dd1c02dbaa45356a3056c'
            'a537cf3423597a0cc673cb4dd9ccb2f132675beb6c923d73325ecf01f6ef84c4'
            'ed600d8a6785b3109c83791701244a00aa1538114678d97bae665794ebc814ac')

package() {
    mkdir -p "$pkgdir/usr/share/${pkgname}"

    cp "$srcdir/${pkgname}".{cue,bin} "$pkgdir/usr/share/${pkgname}"
    cp "$srcdir"/dosbox*.conf "$pkgdir/usr/share/${pkgname}"

    pushd "$pkgdir/usr/share/${pkgname}"
        Xvfb :12356 &
        xpid=$!
        export DISPLAY=:12356

        dosbox --noprimaryconf --conf dosbox-install.conf

        kill $xpid

        cp "$srcdir/Strt3fx.exe" STRT3FX.EXE
    popd

    chown -R root:root "$pkgdir/usr/share/${pkgname}"

    rm -f "$pkgdir/usr/share/${pkgname}"/dosbox-update*.conf
    rm -f "$pkgdir/usr/share/${pkgname}"/dosbox-install.conf

    install -D --mode 755 "$srcdir/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
    install -D --mode 755 "$srcdir/${pkgname}-setup.sh" "${pkgdir}/usr/bin/${pkgname}-setup"
    install -D --mode 755 "$srcdir/${pkgname}-network-server.sh" "${pkgdir}/usr/bin/${pkgname}-network-server"
    install -D --mode 755 "$srcdir/${pkgname}-network-client.sh" "${pkgdir}/usr/bin/${pkgname}-network-client"

    install -D --mode 644 "$srcdir/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
