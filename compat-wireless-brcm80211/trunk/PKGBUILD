# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=compat-wireless-brcm80211
_pkgdate='2010-12-20'
_kernver='2.6.36-ARCH'
pkgver="${_pkgdate//-/.}"
pkgrel=1
pkgdesc='FOSS wireless driver for Broadcom BCM4313, BCM43224, BCM43225 chipsets'
arch=('i686' 'x86_64')
url='http://wireless.kernel.org/en/users/Drivers/brcm80211'
license=('GPL2')
depends=('kernel26' 'linux-firmware')
makedepends=('kernel26-headers')
install="${pkgname}.install"
source=("http://wireless.kernel.org/download/compat-wireless-2.6/compat-wireless-${_pkgdate}.tar.bz2")
sha1sums=('183d84797ccbb1df239cff9d5225426882d7dc7a')

build() {
	cd "${srcdir}/compat-wireless-${_pkgdate}"
	scripts/driver-select brcm80211
	make KLIB="/lib/modules/${_kernver}"
}

package() {
	cd "${srcdir}/compat-wireless-${_pkgdate}"
	make KLIB="/lib/modules/${_kernver}" KMODPATH_ARG="INSTALL_MOD_PATH=${pkgdir}" install-modules
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "${startdir}/${pkgname}.install"
}
