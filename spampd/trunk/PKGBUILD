# Maintainer: Florian "Bluewind" Pritz <flo@xssn.at>
pkgname=spampd
pkgver=2.30
pkgrel=6
pkgdesc="Spamassassin Proxy Daemon"
arch=('any')
url="http://www.worlddesign.com/index.cfm/rd/mta/spampd.htm"
license=('GPL')
depends=('perl' 'perl-net-server' 'spamassassin')
backup=(etc/conf.d/spampd)
install=spampd.install
source=("http://www.worlddesign.com/Content/rd/mta/$pkgname/$pkgname-$pkgver.tar.gz"
    rc-script rc-config spampd.service)
md5sums=('742c6f2cb75db54e59d044a8ee40445f'
         'da17b3904a63252931cfb5f02e6a089f'
         'f452cf5046abc218bffb1e951485d8f0'
         '0ee1229042bf795a22711d6fe819fda0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 spampd "$pkgdir/usr/bin/spampd"
  install -Dm755 "$srcdir/rc-script" "$pkgdir/etc/rc.d/spampd"
  install -Dm644 "$srcdir/rc-config" "$pkgdir/etc/conf.d/spampd"
  install -Dm644 spampd.8.gz "$pkgdir/usr/share/man/man8/spampd.8.gz"
  install -Dm644 "$srcdir/spampd.service" "$pkgdir/usr/lib/systemd/system/spampd.service"
}

# vim:set ts=2 sw=2 et:
