# $Id: PKGBUILD 244856 2015-08-25 14:28:22Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-composer
pkgver=1.0.0.alpha10.465
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='http://getcomposer.org/'
depends=('php>=7')
makedepends=('git')
checkdepends=('mercurial')
source=("${pkgname}::git+https://github.com/composer/composer.git"
        "composer-${pkgver}.phar::https://getcomposer.org/composer.phar")
md5sums=('SKIP'
         'bc7ca705316637ca93b49860926eed98')

pkgver() {
	cd ${srcdir}/${pkgname}

	local _tag=$(git tag -l --sort -version:refname | head -1)
	echo "${_tag}.$(git rev-list ${_tag}.. --count)" | sed 's/-/./g'
}

prepare() {
	cd ${srcdir}/${pkgname}

	php ${srcdir}/composer-${pkgver}.phar -n install --no-dev
}

build() {
	cd ${srcdir}/${pkgname}

	php -dphar.readonly=Off bin/compile
}

check() {
	cd ${srcdir}/${pkgname}

	git config --global user.email "${USER}@${HOST}"
	git config --global user.name "${USER}"

	# install the dev dependencies to run the unit tests
	bin/composer -n install
	vendor/bin/phpunit
}

package() {
	cd ${srcdir}/${pkgname}

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	install -Dm755 composer.phar ${pkgdir}/usr/bin/composer
}
