# $Id: PKGBUILD 155122 2015-12-31 20:05:03Z foutrelis $
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=php-memcache
pkgver=3.0.8
pkgrel=4.1
_commit=88750b4203914bc3c40b485cfa139e3094e0fd04
pkgdesc="Memcache module for PHP"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/memcache"
license=('PHP')
depends=('php')
makedepends=('git')
checkdepends=('memcached')
backup=('etc/php/conf.d/memcache.ini')
install=php-memcache.install
#source=(https://pecl.php.net/get/memcache-$pkgver.tgz)
source=("git+https://github.com/websupport-sk/pecl-memcache.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd "$srcdir/pecl-memcache"

  # Disable UDP tests
  sed -i "s|^\(\$udpPort2\? =\) .*|\1 0;|" tests/connect.inc

  # Remove flaky tests
  rm tests/{036,053,045,051}.phpt
}

build() {
  cd "$srcdir/pecl-memcache"

  phpize
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/pecl-memcache"

  sed -i "s|^\(\$domainsocket =\) .*|\1 'unix://$PWD/memcached.sock';|" \
    tests/connect.inc

  memcached_pids=()
  memcached -p 11211 -U 11211 >/dev/null        & memcached_pids+=($!)
  memcached -p 11212 -U 11212 >/dev/null        & memcached_pids+=($!)
  memcached -s "$PWD/memcached.sock" >/dev/null & memcached_pids+=($!)

  local ret=0
  make test NO_INTERACTION=1 REPORT_EXIT_STATUS=1 || ret=1

  kill ${memcached_pids[@]}

  return $ret
}

package() {
  cd "$srcdir/pecl-memcache"

  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=memcache.so' >memcache.ini
  install -Dm644 memcache.ini "$pkgdir/etc/php/conf.d/memcache.ini"
}

# vim:set ts=2 sw=2 et:
