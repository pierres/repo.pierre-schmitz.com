# $Id: PKGBUILD 158776 2012-05-09 17:05:27Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php53-xcache
pkgver=2.0.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A PHP opcode cacher'
url='http://xcache.lighttpd.net/'
depends=('php53')
conflicts=('php-xcache')
provides=("php-xcache=${pkgver}")
license=('custom')
source=("http://xcache.lighttpd.net/pub/Releases/${pkgver}/xcache-${pkgver}.tar.bz2"
        'xcache.ini')
backup=('etc/php/conf.d/xcache.ini')
md5sums=('0e30cdff075c635e475d70a5c37d0252'
         'bba7ead3e592a7980aa91a8600973587')

build() {
	cd $srcdir/xcache-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

# check() {
# 	cd $srcdir/xcache-$pkgver
# 	make test
# }

package() {
	cd $srcdir/xcache-$pkgver
	make INSTALL_ROOT=$pkgdir install

	install -Dm644 $srcdir/xcache.ini $pkgdir/etc/php/conf.d/xcache.ini

	install -dm755 $pkgdir/usr/share/php-xcache/admin
	install -m644 admin/* $pkgdir/usr/share/php-xcache/admin/

	install -Dm644 COPYING $pkgdir/usr/share/licenses/php-xcache/COPYING
}
