pkgname=gphotos-sync
pkgver=2.14.0
pkgrel=1
pkgdesc='Google Photos and Albums backup tool'
arch=('any')
url='https://github.com/gilesknap/gphotos-sync'
license=('MIT')
depends=('python' 'python-attrs' 'python-exif' 'python-appdirs' 'python-requests-oauthlib' 'python-pyyaml' 'python-psutil')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/gilesknap/gphotos-sync/archive/${pkgver}.tar.gz")
sha256sums=('b99d541191f60aad0f0da68b39b38e20343a7f7eb7b36bd417984a417a17e326')

build() {
	cd "${pkgname}-${pkgver}"

	python setup.py build
}

package() {
	cd "${pkgname}-${pkgver}"

	python setup.py install --root="${pkgdir}" --optimize=1
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
