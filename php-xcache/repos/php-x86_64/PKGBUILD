# $Id: PKGBUILD 175090 2013-01-13 14:38:34Z pierre $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-xcache
pkgver=3.1.0dev
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='A PHP opcode cacher'
url='http://xcache.lighttpd.net/'
depends=('php')
makedepends=('subversion')
license=('custom')
source=("xcache-${pkgver}::svn://svn.lighttpd.net/xcache/trunk/#revision=1249"
        'xcache.ini')
backup=('etc/php/conf.d/xcache.ini')
md5sums=('SKIP'
         '8fd9ce537ada9463c4b0c042243158c0')

build() {
	cd $srcdir/xcache-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

# check() {
# 	cd $srcdir/xcache-$pkgver
# 	make test
# }

package() {
	cd $srcdir/xcache-$pkgver
	make INSTALL_ROOT=$pkgdir install

	install -Dm644 $srcdir/xcache.ini $pkgdir/etc/php/conf.d/xcache.ini

	install -dm755 $pkgdir/usr/share/php-xcache/admin
	cp -r htdocs/* $pkgdir/usr/share/php-xcache/admin/

	install -Dm644 COPYING $pkgdir/usr/share/licenses/php-xcache/COPYING
}
