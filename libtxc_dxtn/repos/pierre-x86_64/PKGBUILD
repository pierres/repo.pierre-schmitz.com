# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=libtxc_dxtn
pkgver=1.0.1
pkgrel=1
arch=(i686 x86_64)
pkgdesc="Texture compression library for Mesa"
url="http://dri.freedesktop.org/wiki/S3TC"
license=(custom:BSD)
depends=(mesa)
options=(!libtool)
source=(http://cgit.freedesktop.org/~mareko/$pkgname/snapshot/$pkgname-$pkgver.tar.bz2)
md5sums=('63dc19246e1b8a5990be5ced66aff913')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # License
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '5,22{s|^ \* \?||;p}' txc_dxtn.h \
    > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
